import com.sun.org.apache.xalan.internal.xsltc.compiler.Copy

plugins {
    id 'com.adarshr.test-logger' version '1.3.1'
    id 'com.avast.gradle.docker-compose' version '0.8.0'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'org.unbroken-dome.test-sets' version '1.5.0'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

testSets {
    integration
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'joda-time:joda-time:2.10'
    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.1'

    testCompile sourceSets.main.output
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
    testCompile 'org.json:json:20160810'
    testCompile 'org.powermock:powermock-release-with-testng-mockito-dependencies:1.6.2'
    testCompile 'org.testng:testng:6.9.12'
    integrationCompile 'org.seleniumhq.selenium:selenium-java:3.13.0'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    destinationDir = file('docs')

    options.header = project.name
    options.windowTitle = "${project.name} - ${project.version}"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

if(!System.properties.containsKey('selenium.debug')) {
    dockerCompose.isRequiredBy integration
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'docs'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

tasks.withType(Test) {
    useTestNG {
        preserveOrder = true
    }

    testlogger {
        showStandardStreams true
        theme 'mocha'
    }
}

integration {
    if(!System.properties.containsKey('selenium.debug')) {
        doFirst {
            def seleniumInfo = dockerCompose.servicesInfos['selenium-hub'].firstContainer

            systemProperty 'selenium.browser', 'chrome'
            systemProperty 'selenium.host', seleniumInfo.host
            systemProperty 'selenium.port', seleniumInfo.ports[4444]
        }
    } else {
        systemProperty 'selenium.debug', System.getProperty('selenium.debug')
    }
}

check.dependsOn integration

jacoco {
    reportsDir = file("$buildDir/coverage")
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
    }
}

task markdown(type: Copy) {
    def date = new Date()
    def licenseYear = date.format('yyyy')

    from './'
    into './'
    include '*.mdt'
    rename '^(.*)\\.mdt$', '$1.md'
    expand(libGroup: libGroup, libName: libName, libVersion: libVersion, year: licenseYear)
    filteringCharset = 'UTF-8'
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true
    publications = ['SmartcarPublication']
    pkg {
        repo = bintrayRepo
        name = libName
        userOrg = bintrayOrg
        licenses = [licenseId]
        desc = libDescription
        websiteUrl = developerWebsite
        issueTrackerUrl = developerIssues
        vcsUrl = vcsRepoUrl
        publicDownloadNumbers = true
        githubRepo = vcsGithubRepo
        version {
            name = libVersion
            vcsTag = 'v' + libVersion
            released = new Date()
        }
    }
}

publishing {
    publications {
        SmartcarPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId libGroup
            artifactId libName
            version libVersion
            pom.withXml {
                def root = asNode()

                root.appendNode('name', libName)
                root.appendNode('description', libDescription)
                root.appendNode('url', libUrl)
                root.appendNode('licenses').
                    appendNode('license').
                        appendNode('name', licenseName).parent().
                        appendNode('url', licenseUrl).parent().
                        appendNode('distribution', 'repo')
                root.appendNode('developers').
                    appendNode('developer').
                        appendNode('id', developerId).parent().
                        appendNode('name', developerName).parent().
                        appendNode('email', developerEmail)
                root.appendNode('scm').
                    appendNode('url', vcsRepoUrl).parent()
            }
        }
    }
}
